<!DOCTYPE html>
<html>
<head>
<title>Memoizacija</title>
<meta charset="utf-8">
<link rel="stylesheet" href="../../pomozno/prosojnice.css" />
</head>
<body>
<textarea id="source">

class: center, middle

# Programiranje 1

## Memoizacija, 2. del

---

### Memoizacija enostavne funkcije

.source[```
let kvadrati = Hashtbl.create 512
let mem_kvadrat x =
  match Hashtbl.find_opt kvadrati x with
  | Some y -> y
  | None ->
      print_endline (string_of_int x);
      let y = x * x in
      Hashtbl.add kvadrati x y;
      y
```]

---

### Memoizacija enostavne rekurzivne funkcije

.source[```
let fibonaccijeva_st = Hashtbl.create 512
let rec mem_fib n =
  match Hashtbl.find_opt fibonaccijeva_st n with
  | Some fn -> fn
  | None ->
      print_endline (string_of_int n);
      let fn =
        match n with
        | 0 | 1 -> n
        | n -> mem_fib (n - 1) + mem_fib (n - 2)
      in
      Hashtbl.add fibonaccijeva_st n fn;
      fn
```]

---

### Memoizacija **poljubne** funkcije

.source[```
let memoiziraj f =
  let rezultati = Hashtbl.create 512 in
  let mem_f x =
    match Hashtbl.find_opt rezultati x with
    | None ->
        let y = f x in
        Hashtbl.add rezultati x y;
        y
    | Some y ->
        y
  in
  mem_f
```]


---

### Memoizacija **poljubne** funkcije

.source[```
let kvadrat x =
  print_endline ("Računam " ^ string_of_int x);
  x * x

let mem_kvadrat = memoiziraj kvadrat
```]

.terminal[```
# mem_kvadrat 10;;
Računam 10
- : int = 100
# mem_kvadrat 10;;
- : int = 100
# mem_kvadrat 5;;
Računam 5
- : int = 25
# mem_kvadrat 5;;
- : int = 25
```]

---

### Memoizacija rekurzivnih funkcij **ne dela** v redu

.source[```
let rec fib n =
  print_int n;
  match n with
  | 0 | 1 -> n
  | n -> fib (n - 1) + fib (n - 2)
let mem_fib = memoiziraj fib
```]

.terminal[```
# mem_fib 6;;
6420131201531201420131201- : int = 8
# mem_fib 6;;
- : int = 8
# mem_fib 5;;
531201420131201- : int = 5
```]

---

### V izračun želimo vriniti drugo funkcijo, zato rekurzivno definicijo **razbijemo** na dva dela

.source[```
let odviti_fib f n =
  print_int n;
  match n with
  | 0 | 1 -> n
  | n -> f (n - 1) + f (n - 2)

let rec fib n = odviti_fib fib n
```]

### Funkcija `fib` je **fiksna točka** funkcije `odviti_fib`

---

### 1. neuspešen poskus

.bad-example[```
let memoiziraj_rec odviti_f =
  let rec mem_f x =
    memoiziraj (fun x -> odviti_f mem_f x) x
  in
  mem_f

let mem_fib = memoiziraj_rec odviti_fib
```]

.terminal[```
# mem_fib 5;;
531201420131201- : int = 5
# mem_fib 5;;
531201420131201- : int = 5
```]

#### Za vsak `x` naredimo novo funkcijo!

---

### 2. neuspešen poskus

.bad-example[```
let memoiziraj_rec odviti_f =
  let rec mem_f =
    memoiziraj (fun x -> odviti_f mem_f x)
  in
  mem_f
```]

.bad-example[```
    memoiziraj (fun x -> odviti_f mem_f x)
    ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
Error: This kind of expression is not allowed
as right-hand side of `let rec'
```]

#### OCaml na desni strani rekurzivnih definicij ne pusti aplikacije!

---

### 3. “uspešen” poskus

.bad-example[```
let memoiziraj_rec odviti_f =
  let fref = ref (fun _ -> assert false) in
  let mem_f =
    memoiziraj (fun x -> odviti_f !fref x)
  in
  fref := mem_f;
  mem_f
```]

#### Gre bolj za hek

---

### 4. uspešen poskus

.source[```
let memoiziraj_rec odviti_f =
  let rezultati = Hashtbl.create 512 in
  let rec mem_f x =
    match Hashtbl.find_opt rezultati x with
    | None ->
        let y = odviti_f mem_f x in
        Hashtbl.add rezultati x y;
        y
    | Some y ->
        y
  in
  mem_f
```]

#### Sicer je malo ponavljanja kode, ampak je najbolj smiselna koda


---

### Koda je zelo podobna običajni memoizaciji

.source[```
let memoiziraj f =
  let rezultati = Hashtbl.create 512 in
  let mem_f x =
    match Hashtbl.find_opt rezultati x with
    | None ->
        let y = f x in
        Hashtbl.add rezultati x y;
        y
    | Some y ->
        y
  in
  mem_f
```]

---

### Za memoizacijo v Pythonu uporabimo **dekoratorje**

.source[```python
def memoiziraj(f):
    rezultati = {}
    def mem_f(x):
        if x not in rezultati:
            rezultati[x] = f(x)
        return rezultati[x]
    return mem_f
```]

---

### Za memoizacijo v Pythonu uporabimo **dekoratorje**

.source[```python
@memoiziraj
def fib(n):
    print(n, end='-')
    if n == 0 or n == 1:
        return n
    else:
        return fib(n - 1) + fib(n - 2)
```]

.terminal[```
>>> fib(10)
10-9-8-7-6-5-4-3-2-1-0-55
```]

#### Zaradi dinamičnosti Pythona težav z rekurzijo ni

---

### Lahko uporabimo tudi dekorator `lru_cache`

.source[```python
from functools import lru_cache


@lru_cache(maxsize=None)
def fib(n):
    print(n, end='-')
    if n == 0 or n == 1:
        return n
    else:
        return fib(n - 1) + fib(n - 2)
```]

.terminal[```
>>> fib(10)
10-9-8-7-6-5-4-3-2-1-0-55
```]

---

### Lahko uporabimo tudi dekorator `lru_cache`

.source[```python
from functools import lru_cache
*memoiziraj = lru_cache(maxsize=None)

*@memoiziraj
def fib(n):
    print(n, end='-')
    if n == 0 or n == 1:
        return n
    else:
        return fib(n - 1) + fib(n - 2)
```]

.terminal[```
>>> fib(10)
10-9-8-7-6-5-4-3-2-1-0-55
```]

---

class: center, middle, question

## Najdaljši podpalindrom

### .large[**o**t**o**ri**n**olar**in**g**o**l**o**g]

### .large[ven**t**r**i**lok**vi**s**t**]

### .large[**neradodaren**]

---
class: question, middle, center

## Najdaljše skupno podzaporedje


### **4**, 5, 2, 1, 9, 0, **4**, **0**, 1, **0**, 7, 5, **9**, 8, **5**, 2, **1**, 6, 8, 9

### 9, 3, **4**, 3, 3, **4**, 6, 3, **0**, **0**, **9**, 2, **5**, **1**, 7, 7, 7, 0, 3, 3

---

class: center, middle, question

## ↓/→ pot z najmanjšo vsoto

|     |     |     |     |
| :-: | :-: | :-: | :-: | :-:
| **131** | 673 | 234 | 103 | 18
| **201** | **96** |  **342** | 965 | 150
| 630 | 803 | **746** | **422** | 111
| 537 | 699 | 497 | **121** | 956
| 805 | 732 | 524 | **37**  | **331**

---

class: center, middle, question

## Število stolpov izmenjujočih barv

.center[![](stolpi-kocke.png)]

.center[![](stolpi-4.png)]



</textarea>
<script src="../../pomozno/prosojnice.js"></script>
</body>
</html>
