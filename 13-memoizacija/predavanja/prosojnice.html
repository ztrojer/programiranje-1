<!DOCTYPE html>
<html>
<head>
<title>Memoizacija</title>
<meta charset="utf-8">
<link rel="stylesheet" href="../../pomozno/prosojnice.css" />
</head>
<body>
<textarea id="source">

class: center, middle

# Programiranje 1

## Memoizacija

---

### Kadar:

1. ima naloga **lastnost optimalne podstrukture** in
2. se podnaloge **prekrivajo**,

.center[![](dinamicno-programiranje.png)]

### govorimo o **dinamičnem programiranju**

---

class: center, middle

### prejšnjič

## primeri optimalnih podstruktur

### danes

## učinkovito reševanje prekrivajočih podnalog

---

class: center, middle

## Najdaljše skupno podzaporedje


### **4**, 5, 2, 1, 9, 0, **4**, **0**, 1, **0**, 7, 5, **9**, 8, **5**, 2, **1**, 6, 8, 9

### 9, 3, **4**, 3, 3, **4**, 6, 3, **0**, **0**, **9**, 2, **5**, **1**, 7, 7, 7, 0, 3, 3

---

class: center, middle

## ↓/→ pot z najmanjšo vsoto

|     |     |     |     |
| :-: | :-: | :-: | :-: | :-:
| **131** | 673 | 234 | 103 | 18
| **201** | **96** |  **342** | 965 | 150
| 630 | 803 | **746** | **422** | 111
| 537 | 699 | 497 | **121** | 956
| 805 | 732 | 524 | **37**  | **331**

---

class: center, middle

## Najdaljši podpalindrom

### .large[**o**t**o**ri**n**olar**in**g**o**l**o**g]

### .large[ven**t**r**i**lok**vi**s**t**]

### .large[**neradodaren**]

---

class: center, middle

## Število stolpov izmenjujočih barv

.center[![](stolpi-kocke.png)]

.center[![](stolpi-4.png)]

---

### Ponavljanju se izognemo na **dva načina**

1. rešitve pripravimo v ustreznem vrstnem redu
.center[![](izracun-vnaprej.png)]
2. rešitve shranimo ob prvem izračunu
.center[![](memoizacija.png)]

---

### Memoizacija enostavne funkcije

.source[```
let kvadrati = Hashtbl.create 512
let mem_kvadrat x =
  match Hashtbl.find_opt kvadrati x with
  | Some y -> y
  | None ->
      print_endline (string_of_int x);
      let y = x * x in
      Hashtbl.add kvadrati x y;
      y
```]

---

### Memoizacija enostavne rekurzivne funkcije

.source[```
let fibonaccijeva_st = Hashtbl.create 512
let rec mem_fib n =
  match Hashtbl.find_opt fibonaccijeva_st n with
  | Some fn -> fn
  | None ->
      print_endline (string_of_int n);
      let fn =
        match n with
        | 0 | 1 -> n
        | n -> mem_fib (n - 1) + mem_fib (n - 2)
      in
      Hashtbl.add fibonaccijeva_st n fn;
      fn
```]

---

### Memoizacija **poljubne** funkcije

.source[```
let memoiziraj f =
  let rezultati = Hashtbl.create 512 in
  let mem_f x =
    match Hashtbl.find_opt rezultati x with
    | None ->
        let y = f x in
        Hashtbl.add rezultati x y;
        y
    | Some y ->
        y
  in
  mem_f
```]

---

### Memoizacija **poljubne** funkcije

.source[```
let kvadrat x =
  print_endline ("Računam " ^ string_of_int x);
  x * x

let mem_kvadrat = memoiziraj kvadrat
```]

.terminal[```
# mem_kvadrat 10;;
Računam 10
- : int = 100
# mem_kvadrat 10;;
- : int = 100
# mem_kvadrat 5;;
Računam 5
- : int = 25
# mem_kvadrat 5;;
- : int = 25
```]

---

### Memoizacija rekurzivnih funkcij **ne dela** v redu

.source[```
let rec fib n =
  print_int n;
  match n with
  | 0 | 1 -> n
  | n -> fib (n - 1) + fib (n - 2)
let mem_fib = memoiziraj fib
```]

.terminal[```
# mem_fib 6;;
6420131201531201420131201- : int = 8
# mem_fib 6;;
- : int = 8
# mem_fib 5;;
531201420131201- : int = 5
```]

---

### V izračun želimo vriniti drugo funkcijo, zato rekurzivno definicijo **razbijemo** na dva dela

.source[```
let odviti_fib f n =
  print_int n;
  match n with
  | 0 | 1 -> n
  | n -> f (n - 1) + f (n - 2)

let rec fib n = odviti_fib fib n
```]

### Funkcija `fib` je **fiksna točka** funkcije `odviti_fib`

---

### Vozel lahko zavežemo tudi v splošnem

.source[```
let zavezi_vozel odviti_f =
  let rec f x =
    odviti_f f x
  in
  f

let fib = zavezi_vozel (fun fib n ->
  match n with
  | 0 | 1 -> n
  | n -> fib (n - 1) + fib (n - 2)
)
```]

---

### Očitna rekurzivna memoizacija je neuporabna

.bad-example[```
let memoiziraj_rec odviti_f =
  let rec mem_f x =
    memoiziraj (fun x -> odviti_f mem_f x) x
  in
  mem_f

let mem_fib = memoiziraj_rec odviti_fib
```]

.terminal[```
# mem_fib 5;;
531201420131201- : int = 5
# mem_fib 5;;
531201420131201- : int = 5
```]
---

### OCaml ne dovoli izboljšave

.bad-example[```
let memoiziraj_rec odviti_f =
  let rec mem_f =
    memoiziraj (fun x -> odviti_f mem_f x)
  in
  mem_f
```]

.bad-example[```
    memoiziraj (fun x -> odviti_f mem_f x)
    ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
Error: This kind of expression is not allowed
as right-hand side of `let rec'
```]

---

### Vozel lahko zavežemo z referencami

.good-example[```
let memoiziraj_rec odviti_f =
  let fref = ref (fun _ -> assert false) in
  let mem_f =
    memoiziraj (fun x -> odviti_f !fref x)
  in
  fref := mem_f;
  mem_f
```]

---

class: question, middle, center

## Najdaljše skupno podzaporedje


### **4**, 5, 2, 1, 9, 0, **4**, **0**, 1, **0**, 7, 5, **9**, 8, **5**, 2, **1**, 6, 8, 9

### 9, 3, **4**, 3, 3, **4**, 6, 3, **0**, **0**, **9**, 2, **5**, **1**, 7, 7, 7, 0, 3, 3

---

### Memoizacija deluje tudi v Pythonu

.source[```python
kvadrati = {}
def mem_kvadrat(x):
    if x not in kvadrati:
        print('Računam', x)
        y = x ** 2
        kvadrati[x] = y
    return kvadrati[x]
```]

.terminal[```python
>>> mem_kvadrat(10)
Računam 10
100
>>> mem_kvadrat(10)
100
```]
---

### V Pythonu lahko funkcijo ob definiciji **dekoriramo**

.bad-example[```python
def pomozna(x):
    ...

f = deko(pomozna)
```]

.good-example[```python
@deko
def f(x):
    ...
```]

---

class: question, middle, center

## `povej_kaj_racunas`

.left.source[```python
@povej_kaj_racunas
def kvadriraj(x):
    return x ** 2
```]

.left.terminal[```python
>>> kvadriraj(4)
Računam 4
16
>>> kvadriraj(5)
Računam 5
25
```]

---

### Memoizacija v splošnem v Pythonu

.source[```python
def memoiziraj(f):
    rezultati = {}
    def mem_f(x):
        if x not in rezultati:
            rezultati[x] = f(x)
        return rezultati[x]
    return mem_f
```]

---

### Memoizacija v splošnem v Pythonu

.source[```python
@memoiziraj
def fib(n):
    print(n, end='')
    if n == 0 or n == 1:
        return n
    else:
        return fib(n - 1) + fib(n - 2)
```]

.terminal[```
>>> fib(10)
10987654321055
>>> fib(10)
55
```]
---

class: center, middle, question

## Najdaljši podpalindrom

### .large[**o**t**o**ri**n**olar**in**g**o**l**o**g]

### .large[ven**t**r**i**lok**vi**s**t**]

### .large[**neradodaren**]

</textarea>
<script src="../../pomozno/prosojnice.js"></script>
</body>
</html>
